plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    // No additional repositories are needed on forge, Tterrag's has it all.
}

dependencies {
    forge("net.minecraftforge:forge:$minecraft_version-$forge_version")
    common(project(path: ":common", configuration: "namedElements")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    // Create and its dependencies
    modImplementation("com.simibubi.create:create-$minecraft_version:$create_forge_version:slim") { transitive = false }
    modImplementation("com.tterrag.registrate:Registrate:$registrate_forge_version")
    modImplementation("com.jozufozu.flywheel:flywheel-forge-$flywheel_forge_minecraft_version:$flywheel_forge_version")

    // Development QOL
    modLocalRuntime("mezz.jei:jei-$minecraft_version-forge:$jei_version") { transitive = false }

    // if you would like to add integration with JEI, uncomment this line.
//    modCompileOnly("mezz.jei:jei-$minecraft_version:$jei_version:api")
}

processResources {
    def props = new Properties()
    rootProject.file("gradle.properties").withInputStream { props.load(it) }

    //noinspection GroovyAssignabilityCheck
    Map<String, Object> properties = props.clone()
    properties.computeIfPresent("create_forge_version", (key, obj) -> {return obj.split("-")[0]})
    properties.computeIfPresent("forge_version", (key, obj) -> {return obj.split("\\.")[0]})

    filesMatching("META-INF/mods.toml") {
        expand properties
    }

    // We technically only need mod name but whatever
    filesMatching("pack.mcmeta") {
        expand properties
    }
}

loom {
    forge {
        mixinConfig(
                "createcoasters-common.mixins.json",
                "createcoasters.mixins.json"
        )
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

jar {
    archiveClassifier = "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
